class LeaderBaronOfHell : BaronOfHell replaces BaronOfHell {
	Default {
		Health 1000;
		Radius 24;
		Height 64;
		Mass 1000;
		Speed 8;
		PainChance 50;
		Monster;
		+FLOORCLIP;
		SeeSound "baron/sight";
		PainSound "baron/pain";
		DeathSound "baron/death";
		ActiveSound "baron/active";
		Obituary "$OB_BARON";
		HitObituary "$OB_BARONHIT";
	}
	
	States {
		Spawn:
			BOSS AB 10 A_Look;
			Loop;
		See:
			TNT1 A 0 Beckon;
			BOSS AABBCCDD 3 A_Chase;
			Loop;
		Melee:
		Missile:
			BOSS EF 8 A_FaceTarget;
			BOSS G 8 A_BruisAttack;
			Goto See;
		Pain:
			BOSS H 2;
			BOSS H 2 A_Pain;
			Goto See;
		Death:
			BOSS I 8;
			BOSS J 8 A_Scream;
			BOSS K 8;
			BOSS L 8 A_NoBlocking;
			BOSS MN 8;
			BOSS O -1 A_BossDeath;
			Stop;
		Raise:
			BOSS O 8;
			BOSS NMLKJI 8;
			Goto See;
	}

	void Beckon() {
		BlockThingsIterator iter = BlockThingsIterator.Create(self, 100);

		while (iter.Next()) {
			Actor candidate = iter.thing;

			if (candidate == self) {
				continue;
			}

			if (!candidate.bIsMonster) {
				continue;
			}

			if (candidate.goal != null || candidate.target != null) {
				continue;
			}
			
			double candidateDistance = self.Distance3D(candidate);

			if (candidateDistance <= 150) {
				candidate.goal = self;
			}
		}
	}
}